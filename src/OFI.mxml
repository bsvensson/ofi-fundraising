<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009"
               xmlns:s="library://ns.adobe.com/flex/spark"
               xmlns:mx="library://ns.adobe.com/flex/mx"
               xmlns:esri="http://www.esri.com/2008/ags"
               xmlns:ns="library://ns.adobe.com/flashx/textLayout"
               applicationComplete="application1_applicationCompleteHandler(event)"
               pageTitle="Sponsor land for Orangutans"
               preinitialize="application1_preinitializeHandler(event)">
    <!--
    ///////////////////////////////////////////////////////////////////////////
    // Copyright (c) 2011 OFI. All Rights Reserved.
    //
    // Licensed under the Apache License, Version 2.0 (the "License");
    // you may not use this file except in compliance with the License.
    // You may obtain a copy of the License at
    //
    //    http://www.apache.org/licenses/LICENSE-2.0
    //
    // Unless required by applicable law or agreed to in writing, software
    // distributed under the License is distributed on an "AS IS" BASIS,
    // WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
    // See the License for the specific language governing permissions and
    // limitations under the License.
    ///////////////////////////////////////////////////////////////////////////
    -->
    
    <!--
    This application was created for the Rawa Kuno project:
    http://www.orangutan.org/how-to-help/rawakuno
    
    This application allows users to donate money by "sponsoring"
    a specific part of the map.
    
    Steps for users:
    1. open application
    2. click an available piece of land
    3. click "Add to cart"
    4. click "Go to checkout"
    5. pay.
    
    Application flow:
    1. on startup, find out the available lands
    2. allow users to click
    3. pass along the "shopping cart" to the OFI ecommerce checkout site
    
    About the map:
    * The map is using ArcGIS Server to show both the detailed imagery as well
    as available/sponsored "lots".
    * When zoomed in, the name of the sponsor (or their chosen text) will
    display on "their" land.
    
    Comments:
    The more complicated parts of this application is the integration of
    whatever ecommerce software your organization might be using,
    as well as getting the detailed imagery, setting up the
    database (including all the "lots") and publishing it using
    ArcGIS Server.
    -->
    
    <fx:Style>
        @namespace esri "http://www.esri.com/2008/ags";
        @namespace s "library://ns.adobe.com/flex/spark";
        @namespace mx "library://ns.adobe.com/flex/mx";
        
        /* The "header" of the InfoWindow */
        esri|InfoWindowLabel
        {
            color: white;
            font-size: 20;
            text-align: center;
        }
        
        esri|InfoWindow
        {
            border-thickness: 0;
            font-size: 13;
            upper-left-radius: 15;
            upper-right-radius: 0;
            info-placement: top;
            info-offset-y: 20;
        }
        
        .labelSlogan
        {
            color: #FFFFFF;
            font-size: 15;
            font-style: italic;
            font-weight: bold;
            padding-top: 10;
        }
        .orange
        {
            background-color: #FFAA00;
            border-color: #FFFFFF;
            border-style: solid;
            border-visible: true;
            border-thickness: 2;
        }
        
        mx|Alert
        {
            corner-radius: 15;
            font-size: 13;
            title-style-name : "title";
        }
        
        .title
        {
            font-size:20;
        }
    </fx:Style>
    
    <fx:Script>
        <![CDATA[
            import com.esri.ags.components.supportClasses.InfoWindow;
            import com.esri.ags.events.FeatureLayerEvent;
            import com.esri.ags.events.LayerEvent;
            import com.esri.ags.events.MapEvent;
            import com.esri.ags.events.MapMouseEvent;
            import com.esri.ags.geometry.MapPoint;
            import com.esri.ags.layers.TiledMapServiceLayer;
            import com.esri.ags.renderers.SimpleRenderer;
            import com.esri.ags.symbols.SimpleFillSymbol;
            import com.esri.ags.tasks.QueryTask;
            import com.esri.ags.tasks.supportClasses.Query;
            import com.google.analytics.AnalyticsTracker;
            import com.google.analytics.GATracker;
            
            import mx.controls.Alert;
            import mx.controls.Spacer;
            import mx.events.CloseEvent;
            import mx.events.FlexEvent;
            import mx.rpc.AsyncResponder;
            
            import spark.components.Button;
            import spark.components.HGroup;
            import spark.components.TextInput;
            import spark.utils.TextFlowUtil;
            
            // START - Variables specific to OFI (Orangutan Foundation International) Rawa Kuna project
            private var addToCartURL:String = "http://www.example.org/someEcommercePage";
            private var checkoutURL:String = "http://www.example.org/checkout";
            private const googleAnalyticsID:String = "UA-11111111-1";
            private var googlePagePath:String = "/rawakuno/ofi8";
            // END   - Variables specific to OFI (Orangutan Foundation International) Rawa Kuna project
            
            // START - General Application settings
            private var popupTitle:String = "Sponsor this land";
            private var addtocartLabel:String = "Add to cart";
            private var landaddedLabel:String = "You just added some land to your cart.";
            private var landaddedTitle:String = "Thank you!";
            private var checkoutLabel:String = "Go to Checkout"
            private var continueshoppingLabel:String = "Continue Shopping";
            // END   - General Application settings
            
            private var firstTime:Number=1;
            
            private var cartItems:Number=0;
            
            private var localDebug:Boolean = true;
            
            private var buttonWidth:Number = Alert.buttonWidth;
            private var yesLabel:String = Alert.yesLabel;
            private var noLabel:String = Alert.noLabel;
            
            
            [Embed("nick-head.jpg")]
            [Bindable]public var iconHead:Class;
            
            public var tracker:AnalyticsTracker;
            
            [Bindable]private var currentOBJECTID:Number = 0;
            [Bindable]private var currentProductID:Number = 5121;
            [Bindable]private var variation:Number = 0;
            
            protected function myMap_mapClickHandler(event:MapMouseEvent):void
            {
                // find lots near the mouse click
                queryMapClick.geometry = event.mapPoint;
                clickLayer.selectFeatures(queryMapClick); // default selectionMethod is FeatureLayer.SELECTION_NEW
                myMap.infoWindow.hide();
                tracker.trackPageview( googlePagePath + "/mapClicked" );
            }
            
            protected function clickLayer_selectionCompleteHandler(event:FeatureLayerEvent):void
            {
                // only show infoWindow if a feature was found
                if (event.featureLayer.numGraphics > 0)
                {
                    var vg:VGroup = new VGroup();
                    
                    myMap.infoWindow.label = popupTitle; // (" + event.featureLayer.selectedFeatures[0].attributes.OBJECTID + ")";
                    myMap.infoWindow.styleName = "orange";
                    var lbl:Label = new Label();
                    lbl.styleName = "labelSlogan";
                    var lotArea:Number = Number(event.featureLayer.selectedFeatures[0].attributes.AreaAcre);
                    if (lotArea > 400)
                    {
                        lbl.text="US$ 50,000 will help protect this " + Math.round(lotArea) + " acres of land!";
                        currentProductID = 5121;
                        variation = 207;
                    }
                    else if (lotArea > 150)
                    {
                        lbl.text="US$ 20,000 will help protect this " + Math.round(lotArea) + " acres of land!";
                        currentProductID = 5121;
                        variation = 206;
                    }
                    else if (lotArea > 90)
                    {
                        lbl.text="US$ 10,000 will help protect this " + Math.round(lotArea) + " acres of land!";
                        currentProductID = 5121;
                        variation = 205;
                    }
                    else if (lotArea > 75)
                    {
                        lbl.text="US$ 8,000 will help protect this " + Math.round(lotArea) + " acres of land!";
                        currentProductID = 5121;
                        variation = 204;
                    }
                    else if (lotArea > 45)
                    {
                        lbl.text="US$ 5,000 will help protect this " + Math.round(lotArea) + " acres of land!";
                        currentProductID = 5121;
                        variation = 203;
                    }
                    else if (lotArea > 25)
                    {
                        lbl.text="US$ 3,000 will help protect this " + Math.round(lotArea) + " acres of land!";
                        currentProductID = 5121;
                        variation = 202;
                    }
                    else if (lotArea > 15)
                    {
                        lbl.text="US$ 2,000 will help protect this " + Math.round(lotArea) + " acres of land!";
                        currentProductID = 5121;
                        variation = 201;
                    }
                    else if (lotArea > 8)
                    {
                        lbl.text="US$ 1,000 will help protect this " + Math.round(lotArea) + " acres of land!";
                        currentProductID = 5121;
                        variation = 200;
                    }
                    else if (lotArea > 1.5 )
                    {
                        lbl.text="US$ 200 will help protect this " + Math.round(lotArea) + " acres of land!";
                        currentProductID = 5121;
                        variation = 199;
                    }
                    else
                    {
                        lbl.text="US$ 100 will help protect this one acre of land!";
                        currentProductID = 5121;
                        variation = 198;
                    }
                    
                    var b1:Button = new Button();
                    b1.label = addtocartLabel;
                    b1.addEventListener(MouseEvent.CLICK, btnAddToCart_clickHandler);
                    var b2:Button = new Button();
                    b2.label = "Cancel";
                    b2.addEventListener(MouseEvent.CLICK, btnCloseInfoWindow_clickHandler);
                    var spacer:Spacer = new Spacer();
                    spacer.percentWidth = 100;
                    var hg:HGroup = new HGroup();
                    hg.percentWidth = 100;
                    hg.horizontalAlign = "contentJustify";
                    hg.addElement(b1);
                    hg.addElement(spacer);
                    hg.addElement(b2);
                    vg.addElement(lbl);
                    vg.addElement(hg);
                    myMap.infoWindow.content = vg;
                    currentOBJECTID = event.featureLayer.selectedFeatures[0].attributes.OBJECTID;
                    myMap.infoWindow.show(queryMapClick.geometry as MapPoint);
                }
            }
            
            private function btnAddToCart_clickHandler(event:MouseEvent):void
            {
                var request:URLRequest = new URLRequest();
                request.url = addToCartURL;
                request.method = "POST";
                request.data = new URLVariables("variation[197]=" + variation + "&wpsc_ajax_action=add_to_cart&product_id=5121&is_customisable=true&custom_text=OBJECTID:" + currentOBJECTID + "~~~");
                
                var _loader4addToCart:URLLoader = new URLLoader(request);
                _loader4addToCart.addEventListener(Event.COMPLETE, loader4addToCart_completeHandler);
                _loader4addToCart.addEventListener(IOErrorEvent.IO_ERROR, ioErrorHandler);
                _loader4addToCart.addEventListener(SecurityErrorEvent.SECURITY_ERROR, securityErrorHandler);
                tracker.trackPageview( googlePagePath + "/addToCart" );
            }
            
            private function btnCloseInfoWindow_clickHandler(event:MouseEvent):void
            {
                myMap.infoWindow.hide();
            }
            
            private function ioErrorHandler(e:IOErrorEvent):void {
                Alert.show("Something went wrong.\n\n" + e.text, "IO Error");
                tracker.trackPageview( googlePagePath + "/ioErrorHandler" );
            }
            private function securityErrorHandler(e:SecurityErrorEvent):void {
                Alert.show("Something went wrong.\n\n" + e.text, "Security Error");  // usually sandbox/crossdomain issue
                tracker.trackPageview( googlePagePath + "/securityErrorHandler" );
            }
            
            private function loader4addToCart_completeHandler(event:Event):void
            {
                cartItems++;
                
                // change labels
                Alert.buttonWidth = 130;
                Alert.yesLabel = checkoutLabel;
                Alert.noLabel = continueshoppingLabel;
                Alert.show(
                    landaddedLabel,
                    landaddedTitle,
                    Alert.YES | Alert.NO,
                    null,
                    function(event:CloseEvent):void
                    {
                        if (event.detail == Alert.YES)
                        {
                            navigateToURL(new URLRequest(checkoutURL), "_blank");
                            tracker.trackPageview( googlePagePath + "/clickedForCheckout" );
                        }
                        else
                        {
                            tracker.trackPageview( googlePagePath + "/clickedToContinueShop" );
                        }
                        myMap.infoWindow.hide();
                    },
                    iconHead,
                    Alert.NO);
                // restore labels to normal
                Alert.buttonWidth = buttonWidth;
                Alert.yesLabel = yesLabel;
                Alert.noLabel = noLabel;
            }
            
            protected function clickLayer_loadHandler(event:LayerEvent):void
            {
                // get count for available
                // get count for sponsored
                // display stats when both counts are available
                
                queryTaskAvailable.executeForCount(
                    queryAvailable,
                    new AsyncResponder(onResultA, onFault)
                );
                queryTaskNotAvailable.executeForCount(
                    queryNotAvailable,
                    new AsyncResponder(onResultS, onFault)
                );
                
                var lotsAvailable:Number;
                var lotsSponsored:Number;
                
                function onResultS(numS:Number, token:Object = null):void
                {
                    lotsSponsored = numS;
                    if (lotsAvailable)
                    {
                        displayStats();
                    }
                }
                
                function onResultA(numA:Number, token:Object = null):void
                {
                    lotsAvailable = numA;
                    if (lotsSponsored)
                    {
                        displayStats();
                    }
                }
                function displayStats():void
                {
                    summaryPanel.visible = true;
                    var percent:Number = Number(100*lotsSponsored/(lotsSponsored+lotsAvailable));
                    lblPercent.textFlow = TextFlowUtil.importFromString(
                        Math.floor(percent)
                        + "<span fontSize='35'>."
                        + Math.round(10 * (percent-Math.floor(percent)))
                        + "</span> %"
                    );
                    lblNote.textFlow = TextFlowUtil.importFromString(
                        myThousandsFormatter.format(lotsAvailable)
                        + " of "
                        + myThousandsFormatter.format(lotsAvailable+lotsSponsored)
                        + " lots available"
                    );
                }
                function onFault(info:Object, token:Object = null):void
                {
                    Alert.show(info.toString());
                }
                
            }
            
            protected function application1_preinitializeHandler(event:FlexEvent):void
            {
                try
                {
                    var settingsLoader:URLLoader = new URLLoader();
                    settingsLoader.load(new URLRequest("settings.txt"));
                    settingsLoader.addEventListener(Event.COMPLETE, settingsLoaded);
                }
                catch (error:Error)
                {
                    trace("Unable to load requested document.");
                }
            }
            
            private function settingsLoaded(event:Event):void
            {
                var str:String = event.target.data as String;
                var arr:Array = str.split("\n");
                for (var i:int=0; i<arr.length; i++)
                {
                    arr[i] = arr[i].replace("\r", "");
                    arr[i] = arr[i].split('=');
                    switch (arr[i][0].toString().toLowerCase())
                    {
                        case "popuptitle":
                            popupTitle = arr[i][1];
                            break;
                        case "addtocartlabel":
                            addtocartLabel = arr[i][1];
                            break;
                        case "landaddedlabel":
                            landaddedLabel = arr[i][1];
                            break;
                        case "landaddedtitle":
                            landaddedTitle = arr[i][1];
                            break;
                        case "checkoutlabel":
                            checkoutLabel = arr[i][1];
                            break;
                        case "continueshoppinglabel":
                            continueshoppingLabel = arr[i][1];
                            break;
                        case "availableserviceurl":
                            clickLayer.url = arr[i][1];
                            queryTaskAvailable.url = arr[i][1];
                            break;
                        case "sponsoredserviceurl":
                            queryTaskNotAvailable.url = arr[i][1]
                            break;
                        default:
                            // trace("Unexpected: " + arr[i]);
                            break;
                    }
                }
            }
            
            protected function application1_applicationCompleteHandler(event:FlexEvent):void
            {
                tracker = new GATracker( this, googleAnalyticsID, "AS3", false );
            }
            
        ]]>
    </fx:Script>
    
    <fx:Declarations>
        <mx:DateFormatter id="myDateFormatter"
                          formatString="MMMM D, YYYY"/>
        <mx:NumberFormatter id="myThousandsFormatter"
                            useThousandsSeparator="true"/>
        <esri:Query id="queryMapClick"/>
        <esri:QueryTask id="queryTaskAvailable"/>
        <esri:QueryTask id="queryTaskNotAvailable"/>
        <esri:Query id="queryAvailable"
                    returnGeometry="false"
                    where="1=1"/>
        <esri:Query id="queryNotAvailable"
                    returnGeometry="false"
                    where="1=1"/>
    </fx:Declarations>
    
    <s:VGroup width="100%" height="100%">
        
        <esri:Map id="myMap"
                  level="12"
                  mapClick="myMap_mapClickHandler(event)"
                  openHandCursorVisible="false"
                  wrapAround180="true">
            <esri:lods>
                <esri:LOD resolution="78271.5169639999" scale="295828763.795777"/>
                <esri:LOD resolution="39135.7584820001" scale="147914381.897889"/>
                <esri:LOD resolution="19567.8792409999" scale="73957190.948944"/>
                <esri:LOD resolution="9783.93962049996" scale="36978595.474472"/>
                <esri:LOD resolution="4891.96981024998" scale="18489297.737236"/>
                <esri:LOD resolution="2445.98490512499" scale="9244648.868618"/>
                <esri:LOD resolution="1222.99245256249" scale="4622324.434309"/>
                <esri:LOD resolution="611.49622628138" scale="2311162.217155"/>
                <esri:LOD resolution="305.748113140558" scale="1155581.108577"/>
                <esri:LOD resolution="152.874056570411" scale="577790.554289"/>
                <esri:LOD resolution="76.4370282850732" scale="288895.277144"/>
                <esri:LOD resolution="38.2185141425366" scale="144447.638572"/>
                <esri:LOD resolution="19.1092570712683" scale="72223.819286"/>
                <esri:LOD resolution="9.55462853563415" scale="36111.909643"/>
                <esri:LOD resolution="4.77731426794937" scale="18055.954822"/>
                <esri:LOD resolution="2.38865713397468" scale="9027.977411"/>
                <esri:LOD resolution="1.19432856685505" scale="4513.988705"/>
                <esri:LOD resolution="0.597164283559817" scale="2256.994353"/>
            </esri:lods>
            <esri:extent>
                <esri:Extent xmin="12417000" ymin="-322000" xmax="12421000" ymax="-311700">
                    <esri:SpatialReference wkid="102100"/>
                </esri:Extent>
            </esri:extent>
            <!--<esri:VETiledLayer key="Al0VE_jsmagxk4LFghXGlK5JSHfC0tk-eHRubZv_eMYm6wzgQaStYks8g-wWcgk3"
                               loadError="Alert.show(event.fault.faultString, 'VETiledLayer loadError')"
                               mapStyle="aerialWithLabels"
                               visible="{myMap.scale &gt; 90000}"
                               />-->
            <esri:ArcGISTiledMapServiceLayer
                url="http://server.arcgisonline.com/ArcGIS/rest/services/World_Imagery/MapServer"
                visible="{myMap.scale &gt; 90000}"
                />
            <esri:ArcGISTiledMapServiceLayer
                url="http://conservation2.arcgisonline.com/ArcGIS/rest/services/OFI_Image/MapServer"
                visible="{myMap.scale &lt; 10100000}"
                />
            <esri:ArcGISDynamicMapServiceLayer
                url="http://50.17.189.227/ArcGIS/rest/services/OFIBorneoLandProtection/MapServer"
                imageFormat="png32"
                visible="{myMap.scale &lt; 10100000}"
                />
            <esri:FeatureLayer id="clickLayer"
                               load="trace('loaded: ' + clickLayer.url); clickLayer_loadHandler(event);"
                               loadError="Alert.show(event.fault.faultString, 'FeatureLayer loadError')"
                               outFields="[AreaAcre]"
                               mode="selection"
                               renderer="{new SimpleRenderer( new SimpleFillSymbol('solid',0x0000FF) )}"
                               selectionComplete="clickLayer_selectionCompleteHandler(event)"
                               />
        </esri:Map>
    </s:VGroup>
    
    <s:Label backgroundColor="white"
             bottom="2"
             left="2"
             visible="{myMap.loaded}"
             text=" Current map scale is 1:{myThousandsFormatter.format(Math.round(myMap.scale))} "/>
    
    <s:Panel id="summaryPanel"
             textAlign="center"
             right="10" top="10"
             title="Current Status"
             visible="false">
        <s:RichText id="lblPercent"
                    fontSize="70"
                    textAlign="center"
                    top="4"
                    />
        <!-- Visible not working here??? -->
        <s:Label fontSize="24"
                 text="sponsored so far"
                 textAlign="center"
                 top="58"
                 />
        <s:RichText id="lblNote"
                    fontSize="13"
                    fontStyle="italic"
                    top="80"
                    />
    </s:Panel>
</s:Application>